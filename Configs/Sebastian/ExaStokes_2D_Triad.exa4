Domain global< [ 0, 0 ] to [ 1, 1 ] >

Layout DefCellLayout < Real , Cell > @all {
	duplicateLayers	= [ 0, 0 ]
	ghostLayers		= [ 1, 1 ] with communication
}

Layout DefFaceXLayout < Real , Face_x > @all {
	duplicateLayers	= [ 1, 0 ] with communication
	ghostLayers		= [ 1, 1 ] with communication
}

Layout DefFaceYLayout < Real , Face_y > @all {
	duplicateLayers	= [ 0, 1 ] with communication
	ghostLayers		= [ 1, 1 ] with communication
}

// values associated with u, ie face_x

Field u < global, DefFaceXLayout, ApplyBC_u@finest( ) > @finest
Field u < global, DefFaceXLayout, 0.0 > @(coarsest to (finest - 1))

Field residual_u < global, DefFaceXLayout, 0.0 > @all
Field rhs_u < global, DefFaceXLayout, None > @all

// values associated with v, ie face_y

Field v < global, DefFaceYLayout, 0.0 > @all

Field residual_v < global, DefFaceYLayout, 0.0 > @all
Field rhs_v < global, DefFaceYLayout, None > @all

// values associated with p and rho, ie cell center

Field p < global, DefCellLayout, None > @all // not evaluated outside inner scope

Field residual_p < global, DefCellLayout, None > @all
Field rhs_p < global, DefCellLayout, None > @all

// stencils and stencils templates

Stencil Laplace@all {
	[ 0,  0] =>  2.0 / ( vf_gridWidth_x@current ** 2 ) + 2.0 / ( vf_gridWidth_y@current ** 2 )
	[-1,  0] => -1.0 / ( vf_gridWidth_x@current ** 2 )
	[ 1,  0] => -1.0 / ( vf_gridWidth_x@current ** 2 )
	[ 0, -1] => -1.0 / ( vf_gridWidth_y@current ** 2 )
	[ 0,  1] => -1.0 / ( vf_gridWidth_y@current ** 2 )
}

Stencil dxLeft@all {
	[-1,  0] => -1.0 / vf_gridWidth_x@current
	[ 0,  0] =>  1.0 / vf_gridWidth_x@current
}

Stencil dxRight@all {
	[ 0,  0] => -1.0 / vf_gridWidth_x@current
	[ 1,  0] =>  1.0 / vf_gridWidth_x@current
}

Stencil dyLeft@all {
	[ 0, -1] => -1.0 / vf_gridWidth_y@current
	[ 0,  0] =>  1.0 / vf_gridWidth_y@current
}

Stencil dyRight@all {
	[ 0,  0] => -1.0 / vf_gridWidth_y@current
	[ 0,  1] =>  1.0 / vf_gridWidth_y@current
}

Stencil RestrictionCell@all {
	[ 0,  0] => 0.25
    [ 1,  0] => 0.25
    [ 0,  1] => 0.25
    [ 1,  1] => 0.25
}

Stencil CorrectionCell@all {
	[ 0,  0] => 1.0
}

Stencil RestrictionFaceX@all {
	[ 0,  0] => 0.25
	[-1,  0] => 0.125
	[ 1,  0] => 0.125

	[ 0,  1] => 0.25
	[-1,  1] => 0.125
	[ 1,  1] => 0.125
}

Stencil CorrectionFaceX@all {
	[    0,  0] => 0.5
	[x % 2,  0] => 0.5
}

Stencil RestrictionFaceY@all {
	[ 0,  0] => 0.25
	[ 0, -1] => 0.125
	[ 0,  1] => 0.125

	[ 1,  0] => 0.25
	[ 1, -1] => 0.125
	[ 1,  1] => 0.125
}

Stencil CorrectionFaceY@all {
	[ 0,     0] => 0.5
	[ 0, y % 2] => 0.5
}

/// Globals

Globals {
	Var omega : Real = 0.7
}

/// BC functions

Function ApplyBC_u@finest ( ) : Unit {
	Val topVel : Real = 0.02 // * sin ( PI * vf_nodePosition_x@current ) // 0.0025
	loop over u@current only ghost [0,  1] on boundary {
		u@current = 2.0 * topVel - u@current@[0, -1]
	}
	loop over u@current only ghost [0, -1] on boundary {
		u@current = -u@current@[0,  1]
	}
	loop over u@current only dup [-1, 0] on boundary {
		u@current = 0.
	}
	loop over u@current only dup [ 1, 0] on boundary {
		u@current = 0.
	}
}

/// main functions

Function Application ( ) : Unit {
	startTimer ( 'setup' )

	initGlobals ( )
	initDomain ( )
	initFieldsWithZero ( )
	initGeometry ( )
	InitFields@finest ( )

	stopTimer ( 'setup' )

	Solve@finest ( )

	//printAllTimers ( )
	destroyGlobals ( )
}

Function InitFields@finest ( ) : Unit {
	//loop over p@current {
	//	p@current = 1.0
	//}
	
	apply bc to u@current
	apply bc to v@current
	apply bc to p@current
}

Function Solve@finest ( ) : Unit {
	// TODO: merge into norm
	UpdateResidual@finest ( )
	Var initRes : Real    = NormResidual_max@current()
	Var curRes  : Real    = initRes
	Var prevRes : Real    = curRes
	Var numIt   : Integer = 0

	//print ( 'Starting residual at 0', initRes )
	printVal ( initRes )
	startTimer ( 'timeToSolve' )

	// TODO: check with Lisa
	repeat until ( curRes <= 1e-6 /* initRes */ || numIt >= 10 ) {
		startTimer ( 'cycle' )
		VCycle@finest (  )
		stopTimer ( 'cycle' )

		if (getTotalFromTimer ( 'cycle' ) > 1200000 ) {
			print ( 'Aborting solve after', getTotalFromTimer ( 'cycle' ), 'ms which exceeds the limit of', 1200000, 'ms' )
			return
		}

		startTimer ( 'convergenceChecking' )
		prevRes = curRes
		// TODO: merge into norm
		UpdateResidual@finest ( )
		curRes = NormResidual_max@current()
		//print ( 'Residual:', curRes, 'Residual reduction:', ( initRes / curRes ), 'Convergence factor:', ( curRes / prevRes ) )
		numIt += 1
		stopTimer ( 'convergenceChecking' )
	}

	stopTimer ( 'timeToSolve' )
	//print ( 'Total time to solve in', numIt, 'steps :', getTotalFromTimer ( 'timeToSolve' ) )
	//print ( 'Mean time per vCycle: ', getMeanFromTimer ( 'cycle' ) )
	
	if ( false ) {
		printField ( "u.csv", u@finest )
		printField ( "ru.csv", residual_u@finest )
		printField ( "v.csv", v@finest )
		printField ( "rv.csv", residual_v@finest )
		printField ( "p.csv", p@finest )
	}
}

Function printVal ( toPrint : Real ) : Unit {
    if ( toPrint <= 1e-12 ) {
		print ( "EFFECTIVELY ZERO" )
    } else if ( toPrint <= 1e-11 ) {
		native ( 'std::streamsize oldPrec = std::cout.precision()' )
		native ( 'std::cout.precision(1)' )
		print ( toPrint )
		native ( 'std::cout.precision(oldPrec)' )
    } else if ( toPrint <= 1e-10 ) {
		native ( 'std::streamsize oldPrec = std::cout.precision()' )
		native ( 'std::cout.precision(2)' )
		print ( toPrint )
		native ( 'std::cout.precision(oldPrec)' )
    } else if ( toPrint <= 1e-9 ) {
		native ( 'std::streamsize oldPrec = std::cout.precision()' )
		native ( 'std::cout.precision(3)' )
		print ( toPrint )
		native ( 'std::cout.precision(oldPrec)' )
    } else {
		native ( 'std::streamsize oldPrec = std::cout.precision()' )
		native ( 'std::cout.precision(4)' )
		print ( toPrint )
		native ( 'std::cout.precision(oldPrec)' )
    }
}

Function NormResidual_max@finest ( ) : Real {
	Var res_u : Real = NormResidual_u@current ( )
	Var res_v : Real = NormResidual_v@current ( )
	Var res_p : Real = NormResidual_p@current ( )

	printVal ( res_u )
	printVal ( res_v )
	printVal ( res_p )
	//print ( 'res_u', res_u )
	//print ( 'res_v', res_v )
	//print ( 'res_p', res_p )

	// TODO: check with Lisa
	return max ( res_u, res_v, res_p )
}

Function VCycle@((coarsest + 1) to finest) ( ) : Unit {
	repeat 2 times {
		Smoother@current ( )
	}
	UpdateResidual@current ( )
	Restriction@current ( )
	SetSolution@coarser ( 0 )
	VCycle@coarser ( )
	Correction@current ( )
	repeat 2 times {
		Smoother@current ( )
	}
}

Function VCycle@coarsest ( ) : Unit {
	// TODO: implement real cgs
	repeat 10 times {
		Smoother@current ( )
	}
}

/*Function Smoother@(coarsest to finest - 1) ( ) : Unit {
	color with {
		( 0 == x % 3 && 0 == y % 3) ,
		( 1 == x % 3 && 0 == y % 3) ,
		( 2 == x % 3 && 0 == y % 3) ,
		( 0 == x % 3 && 1 == y % 3) ,
		( 1 == x % 3 && 1 == y % 3) ,
		( 2 == x % 3 && 1 == y % 3) ,
		( 0 == x % 3 && 2 == y % 3) ,
		( 1 == x % 3 && 2 == y % 3) ,
		( 2 == x % 3 && 2 == y % 3) ,

		loop over p@current {
			solve locally relax 0.8 {
				u@current@[0, 0] => Laplace@current * u@current@[0, 0] - dxLeft@current  * p@current@[0, 0] == rhs_u@current@[0, 0]
				u@current@[1, 0] => Laplace@current * u@current@[1, 0] - dxLeft@current  * p@current@[1, 0] == rhs_u@current@[1, 0]
				v@current@[0, 0] => Laplace@current * v@current@[0, 0] - dyLeft@current  * p@current@[0, 0] == rhs_v@current@[0, 0]
				v@current@[0, 1] => Laplace@current * v@current@[0, 1] - dyLeft@current  * p@current@[0, 1] == rhs_v@current@[0, 1]
				p@current@[0, 0] => dxRight@current * u@current@[0, 0] + dyRight@current * v@current@[0, 0] == rhs_p@current@[0, 0]
			}
		}
	}
}*/

Function Smoother@all ( ) : Unit {
	Var it : Int = 0
	repeat 4 times count it {
		if ( 0 == it % 4 ) {
			color with {
				( 0 == (x + y) % 2 ) ,
				( 1 == (x + y) % 2 ) ,

			loop over p@current { // { starting [0, 1] ending [1, 0] {
				if ( ! ( isOnBoundaryOf ( u@current@[1, 0] ) && isOnBoundaryOf ( v@current@[0, 0] ) ) ) {
				solve locally relax omega {
					//u@current@[0, 0] => Laplace@current * u@current@[0, 0] - dxLeft@current  * p@current@[0, 0] == rhs_u@current@[0, 0]
					u@current@[1, 0] => Laplace@current * u@current@[1, 0] - dxLeft@current  * p@current@[1, 0] == rhs_u@current@[1, 0]
					v@current@[0, 0] => Laplace@current * v@current@[0, 0] - dyLeft@current  * p@current@[0, 0] == rhs_v@current@[0, 0]
					//v@current@[0, 1] => Laplace@current * v@current@[0, 1] - dyLeft@current  * p@current@[0, 1] == rhs_v@current@[0, 1]
					p@current@[0, 0] => dxRight@current * u@current@[0, 0] + dyRight@current * v@current@[0, 0] == rhs_p@current@[0, 0]
				}
				}
			}
			}
		}
		
		if ( 1 == it % 4 ) {
			color with {
				( 0 == (x + y) % 2 ) ,
				( 1 == (x + y) % 2 ) ,

			loop over p@current { // { starting [1, 0] ending [0, 1] {
				if ( ! ( isOnBoundaryOf ( u@current@[0, 0] ) && isOnBoundaryOf ( v@current@[0, 1] ) ) ) {
				solve locally relax omega {
					u@current@[0, 0] => Laplace@current * u@current@[0, 0] - dxLeft@current  * p@current@[0, 0] == rhs_u@current@[0, 0]
					//u@current@[1, 0] => Laplace@current * u@current@[1, 0] - dxLeft@current  * p@current@[1, 0] == rhs_u@current@[1, 0]
					//v@current@[0, 0] => Laplace@current * v@current@[0, 0] - dyLeft@current  * p@current@[0, 0] == rhs_v@current@[0, 0]
					v@current@[0, 1] => Laplace@current * v@current@[0, 1] - dyLeft@current  * p@current@[0, 1] == rhs_v@current@[0, 1]
					p@current@[0, 0] => dxRight@current * u@current@[0, 0] + dyRight@current * v@current@[0, 0] == rhs_p@current@[0, 0]
				}
				}
			}
			}
		}
		
		if ( 2 == it % 4 ) {
			color with {
				( 0 == (x + y) % 2 ) ,
				( 1 == (x + y) % 2 ) ,

			loop over p@current { // { starting [0, 0] ending [1, 1] {
				if ( ! ( isOnBoundaryOf ( u@current@[1, 0] ) && isOnBoundaryOf ( v@current@[0, 1] ) ) ) {
				solve locally relax omega {
					//u@current@[0, 0] => Laplace@current * u@current@[0, 0] - dxLeft@current  * p@current@[0, 0] == rhs_u@current@[0, 0]
					u@current@[1, 0] => Laplace@current * u@current@[1, 0] - dxLeft@current  * p@current@[1, 0] == rhs_u@current@[1, 0]
					//v@current@[0, 0] => Laplace@current * v@current@[0, 0] - dyLeft@current  * p@current@[0, 0] == rhs_v@current@[0, 0]
					v@current@[0, 1] => Laplace@current * v@current@[0, 1] - dyLeft@current  * p@current@[0, 1] == rhs_v@current@[0, 1]
					p@current@[0, 0] => dxRight@current * u@current@[0, 0] + dyRight@current * v@current@[0, 0] == rhs_p@current@[0, 0]
				}
				}
			}
			}
		}
		
		if ( 3 == it % 4 ) {
			color with {
				( 0 == (x + y) % 2 ) ,
				( 1 == (x + y) % 2 ) ,

			loop over p@current { // { starting [1, 1] ending [0, 0] {
				if ( ! ( isOnBoundaryOf ( u@current@[0, 0] ) && isOnBoundaryOf ( v@current@[0, 0] ) ) ) {
				solve locally relax omega {
					u@current@[0, 0] => Laplace@current * u@current@[0, 0] - dxLeft@current  * p@current@[0, 0] == rhs_u@current@[0, 0]
					//u@current@[1, 0] => Laplace@current * u@current@[1, 0] - dxLeft@current  * p@current@[1, 0] == rhs_u@current@[1, 0]
					v@current@[0, 0] => Laplace@current * v@current@[0, 0] - dyLeft@current  * p@current@[0, 0] == rhs_v@current@[0, 0]
					//v@current@[0, 1] => Laplace@current * v@current@[0, 1] - dyLeft@current  * p@current@[0, 1] == rhs_v@current@[0, 1]
					p@current@[0, 0] => dxRight@current * u@current@[0, 0] + dyRight@current * v@current@[0, 0] == rhs_p@current@[0, 0]
				}
				}
			}
			}
		}

		communicate u@current
		communicate v@current
		communicate p@current

		apply bc to u@current
		apply bc to v@current
		apply bc to p@current
	}
}

Function UpdateResidual@all ( ) : Unit {
	// just call component-wise functions
	UpdateResidual_u@current ( )
	UpdateResidual_v@current ( )
	UpdateResidual_p@current ( )
}

Function UpdateResidual_u@all ( ) : Unit {
	loop over residual_u@current {
		residual_u@current = rhs_u@current - ( Laplace@current * u@current - dxLeft@current * p@current )
	}

	communicate residual_u@current
}

Function UpdateResidual_v@all ( ) : Unit {
	loop over residual_v@current {
		residual_v@current = rhs_v@current - ( Laplace@current * v@current - dyLeft@current * p@current )
	}

	communicate residual_v@current
}

Function UpdateResidual_p@all ( ) : Unit {
	loop over residual_p@current {
		residual_p@current = rhs_p@current - ( dxRight@current * u@current + dyRight@current * v@current )
	}

	communicate residual_p@current
}

Function Restriction@( ( coarsest + 1 ) to finest ) ( ) : Real {
	// just call component-wise functions
	Restriction_u@current ( )
	Restriction_v@current ( )
	Restriction_p@current ( )
}

Function Correction@( ( coarsest + 1 ) to finest ) ( ) : Real {
	// just call component-wise functions
	Correction_u@current ( )
	Correction_v@current ( )
	Correction_p@current ( )
}

Function SetSolution@all ( value : Real ) : Real {
	// just call component-wise functions
	SetSolution_u@current ( value )
	SetSolution_v@current ( value )
	SetSolution_p@current ( value )
}

/// templates for (multigrid) solver components

FunctionTemplate NormResidualComponent < residual > ( ) : Real {
	Variable totalRes : Real = 0
	loop over residual with reduction( + : totalRes ) {
		totalRes += residual ** 2
	}
	return sqrt ( totalRes )
}

FunctionTemplate UpdateResidualComponent < solution, stencil, rhs, residual > ( ) : Unit {
	loop over residual {
		residual = rhs - stencil * solution
	}
	communicate residual
}

FunctionTemplate RestrictionComponent < source, dest, restrictionStencil > ( ) : Unit {
	loop over dest {
		dest = restrictionStencil * source
	}
}

FunctionTemplate CorrectionComponent < source, dest, correctionStencil, requiresBcUpdate > ( ) : Unit {
	loop over dest {
		dest += correctionStencil * source
	}

	communicate dest
	if ( requiresBcUpdate ) {
		apply bc to dest
	}
}

FunctionTemplate SetFieldComponent < target > ( value : Real ) : Unit {
	loop over target starting [-1, -1] ending [-1, -1] {
		target = value
	}
}

// instantiations

Instantiate NormResidualComponent < residual_u@current > as NormResidual_u@finest
Instantiate NormResidualComponent < residual_v@current > as NormResidual_v@finest
Instantiate NormResidualComponent < residual_p@current > as NormResidual_p@finest

Instantiate RestrictionComponent < residual_u@current, rhs_u@coarser, RestrictionFaceX@current > as Restriction_u@( ( coarsest + 1 ) to finest )
Instantiate RestrictionComponent < residual_v@current, rhs_v@coarser, RestrictionFaceY@current > as Restriction_v@( ( coarsest + 1 ) to finest )
Instantiate RestrictionComponent < residual_p@current, rhs_p@coarser, RestrictionCell@current > as Restriction_p@( ( coarsest + 1 ) to finest )

Instantiate CorrectionComponent < u[active]@coarser, u[active]@current, CorrectionFaceX@current, true > as Correction_u@( ( coarsest + 1 ) to finest )
Instantiate CorrectionComponent < v[active]@coarser, v[active]@current, CorrectionFaceY@current, true > as Correction_v@( ( coarsest + 1 ) to finest )
Instantiate CorrectionComponent < p[active]@coarser, p[active]@current, CorrectionCell@current, true > as Correction_p@( ( coarsest + 1 ) to finest )

Instantiate SetFieldComponent < u[active]@current > as SetSolution_u@all
Instantiate SetFieldComponent < v[active]@current > as SetSolution_v@all
Instantiate SetFieldComponent < p[active]@current > as SetSolution_p@all
