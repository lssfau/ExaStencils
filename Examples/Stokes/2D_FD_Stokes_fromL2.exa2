
Domain global< [ 0, 0 ] to [ 1, 1 ] >


// u     = sin ( 2.0 * PI * x ) - cos (       PI * y )
// v     = cos (       PI * x ) - sin ( 2.0 * PI * y )
// p     = sin ( 4.0 * PI * x ) + sin ( 4.0 * PI * y )
// rhs_u = 4.0 * PI**2 * sin ( 2.0 * PI * x ) -       PI**2 * cos (       PI * y ) + 4.0 * PI * cos ( 4.0 * PI * x )
// rhs_v =       PI**2 * cos (       PI * x ) - 4.0 * PI**2 * sin ( 2.0 * PI * y ) + 4.0 * PI * cos ( 4.0 * PI * y )
// rhs_p = 2.0 * PI    * cos ( 2.0 * PI * x ) - 2.0 * PI    * cos ( 2.0 * PI * y ) 

Field u with Real on Face_x of global = 0.0
Field u on boundary = sin ( 2.0 * PI * vf_boundaryPosition_x ) - cos ( PI * vf_boundaryCoord_y )

Field v with Real on Face_y of global = 0.0
Field v on boundary = cos ( PI * vf_boundaryPosition_x ) - sin ( 2.0 * PI * vf_boundaryCoord_y )

Field p with Real on Cell of global = 0.0
Field p on boundary = Neumann // TODO: currently not evaluated outside inner scope

Field rhs_u with Real on Face_x of global = 4.0 * PI**2 * sin ( 2.0 * PI * vf_nodePosition_x ) -       PI**2 * cos (       PI * vf_cellCenter_y   ) + 4.0 * PI * cos ( 4.0 * PI * vf_nodePosition_x )
Field rhs_v with Real on Face_y of global =       PI**2 * cos (       PI * vf_cellCenter_x   ) - 4.0 * PI**2 * sin ( 2.0 * PI * vf_nodePosition_y ) + 4.0 * PI * cos ( 4.0 * PI * vf_nodePosition_y )
Field rhs_p with Real on Cell   of global = 2.0 * PI    * cos ( 2.0 * PI * vf_cellCenter_x   ) - 2.0 * PI    * cos ( 2.0 * PI * vf_cellCenter_y   )


Globals {
  Expr dAInv = 1.0 // vf_gridWidth_x@finest // 3e-2 // ( ( 2.0 / ( vf_gridWidth_x@finest ** 2 ) + 2.0 / ( vf_gridWidth_y@finest ** 2 ) ) )
  Expr dDInv = 1.0 // vf_gridWidth_x@coarsest // ( vf_gridWidth_x@finest )**2 * dAInv 
}


Operator Laplace from Stencil {
  [ 0,  0] =>  2.0 / ( vf_gridWidth_x ** 2 ) + 2.0 / ( vf_gridWidth_y ** 2 )
  [-1,  0] => -1.0 / ( vf_gridWidth_x ** 2 )
  [ 1,  0] => -1.0 / ( vf_gridWidth_x ** 2 )
  [ 0, -1] => -1.0 / ( vf_gridWidth_y ** 2 )
  [ 0,  1] => -1.0 / ( vf_gridWidth_y ** 2 )
}


// TODO: adapt for non-uniform grids
Equation uEquation {
  dAInv * ( Laplace * u ) + dAInv * ( ( p@[0, 0] - p @[-1, 0] ) / vf_gridWidth_x ) == dAInv * rhs_u
  //vf_gridWidth_x * Laplace * u + p@[0, 0] - p @[-1, 0] == vf_gridWidth_x * rhs_u
}

Equation vEquation {
  dAInv * ( Laplace * v ) + dAInv * ( ( p@[0, 0] - p @[0, -1] ) / vf_gridWidth_y ) == dAInv * rhs_v
  //vf_gridWidth_y * Laplace * v + p@[0, 0] - p @[0, -1] == vf_gridWidth_y * rhs_v
}

Equation pEquation {
  dDInv * ( ( u@[1, 0] - u @[0, 0] ) / vf_gridWidth_x + ( v@[0, 1] - v @[0, 0] ) / vf_gridWidth_y ) == dDInv * rhs_p
  //( ( u@[1, 0] - u @[0, 0] ) / vf_gridWidth_x + ( v@[0, 1] - v @[0, 0] ) / vf_gridWidth_y ) == rhs_p
}


generate operators @all {
  equation for u is uEquation store in {
    u => A11
    p => B1
  }

  equation for v is vEquation store in {
    v => A22
    p => B2
  }

  equation for p is pEquation store in {
    u => C1
    v => C2
  }
}
