/// inline knowledge

Knowledge {
  dimensionality = 2

  minLevel       = 0
  maxLevel       = 8
}

/// problem specification

\Omega = ( 0, 1 ) \times ( 0, 1 )

f \in \Omega = PI**2 * cos ( PI * x ) - 4.0 * PI**2 * sin ( 2.0 * PI * y )
u \in \Omega = 0.0
u \in \partial \Omega = cos ( PI * x ) - sin ( 2.0 * PI * y )

op = - \Delta // alt: - ( \partial_{xx} + \partial_{yy} )

uEq: f = op * u

/// configuration of inter-layer transformations

DiscretizationHints { // alt: Discretize, L2Hint(s)
  f on Node
  u on Node

  op on \Omega

  uEq

  // parameters
  discr_type = "FiniteDifferences"
}

SolverHints { // alt: Solve, L3Hint(s)
  generate solver for u in uEq

  // parameters
  solver_targetResReduction = 1e-10
}

ApplicationHints { // alt L4Hint(s)
  // parameters
  l4_genDefaultApplication = true
}
