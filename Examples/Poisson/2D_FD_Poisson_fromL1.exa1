/// inline knowledge

Knowledge {
  dimensionality = 2

  minLevel       = 2
  maxLevel       = 8
}

/// problem specification

Domain global = ( 0, 1 ) \times ( 0, 1 )

Field f@finest \in global = 0.0
Field u \in global = 0.0

Field u@finest \in \partial global = vf_boundaryCoord_x ** 2 - vf_boundaryCoord_y ** 2 // x ** 2 - y ** 2
Field u@(all but finest) \in \partial global = 0.0

Operator op = - \Delta // alt: - ( \partial_{xx} + \partial_{yy} )

Equation uEq@finest           op * u == f
Equation uEq@(all but finest) op * u == 0.0

/// configuration of inter-layer transformations

DiscretizationHints { // alt: Discretize, L2Hint(s)
  f on Node
  u on Node

  op on global

  uEq

  // paramters
  discr_type = "FiniteDifferences"
}

SolverHints { // alt: Solve, L3Hint(s)
  generate solver for u in uEq

  // parameters
  solver_targetResReduction = 1e-6
}

ApplicationHints { // alt L4Hint(s)
  // parameters
  l4_genDefaultApplication = true
}
