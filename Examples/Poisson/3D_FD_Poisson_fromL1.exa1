/// inline knowledge

Knowledge {
  dimensionality = 3

  minLevel       = 2
  maxLevel       = 6
}

/// problem specification

Domain \Omega = ( 0, 1 ) \times ( 0, 1 ) \times ( 0, 1 )

Field f@finest \in \Omega = 0.0
Field u \in \Omega = 0.0

Field u@finest \in \partial \Omega = x * x - 0.5 * y * y - 0.5 * z * z
Field u@(all but finest) \in \partial \Omega = 0.0

Operator op = - \Delta // alt: - ( \partial_{xx} + \partial_{yy} + \partial_{zz} )

Equation uEq@finest           op * u == f
Equation uEq@(all but finest) op * u == 0.0

/// configuration of inter-layer transformations

DiscretizationHints { // alt: Discretize, L2Hint(s)
  f on Node
  u on Node

  op on \Omega

  uEq

  // paramters
  discr_type = "FiniteDifferences"
}

SolverHints { // alt: Solve, L3Hint(s)
  generate solver for u in uEq

  // parameters
  solver_targetResReduction = 1e-6
}

ApplicationHints { // alt L4Hint(s)
  // parameters
  l4_genDefaultApplication = true
}
